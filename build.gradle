import org.gradle.internal.jvm.Jvm
import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'application'
    id 'c'
    id 'com.adarshr.test-logger' version '2.0.0'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '0.7'
}

group 'org.example'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    jaxb
}

application {
    mainClassName = 'gui.JSpeccy'
    applicationDefaultJvmArgs = ['-DNATIVE_LIB_PLACEHOLDER']
}

distributions.main.contents {
    from("native") {
        into "native"
        include "**/*.dll"
        include "**/*.so"
    }
}

startScripts.doLast {
    unixScript.text = unixScript.text.replace(
            'NATIVE_LIB_PLACEHOLDER',
            'java.library.path=\$APP_HOME/native/linux')
    windowsScript.text = windowsScript.text.replace(
            'NATIVE_LIB_PLACEHOLDER',
            'java.library.path=%APP_HOME%\\native/windows')
}

idea.project.settings.encodings {
    encoding = 'UTF-8'
    properties {
        encoding = 'US-ASCII'
        transparentNativeToAsciiConversion = true
    }
}

task genJSpeccySettingsClasses {
    System.setProperty('javax.xml.accessExternalSchema', 'all')
    def jaxbTargetDir = file("build/generated/sources/JSpeccySettings/")
    doLast {
        jaxbTargetDir.mkdirs()
        ant.taskdef(
                name: 'xjc',
                classname: 'com.sun.tools.xjc.XJCTask',
                classpath: configurations.jaxb.asPath
        )
        ant.jaxbTargetDir = jaxbTargetDir
        ant.xjc(
                destdir: '${jaxbTargetDir}',
                package: 'configuration',
                schema: 'xml-resources/jaxb/JSpeccySettingsType/JSpeccy.xsd',
                language: 'WSDL'
        )
    }
}

compileJava {
    // Also, for NetBeans editor needs to add "-J-Dfile.encoding=UTF-8"
    // to parameter "netbeans_default_options" in "etc/nebeans.conf".
    //
    // Gradle / Netbeans - Howto set encoding to UTF-8 in Editor and Compiler
    // https://stackoverflow.com/questions/59800221/gradle-netbeans-howto-set-encoding-to-utf-8-in-editor-and-compiler
    options.encoding = 'UTF-8'
    options.compilerArgs += [
            // to generate joystickinput_JoystickRaw.h header
            "-h", file("native/" + OperatingSystem.current().familyName)
    ]
    source genJSpeccySettingsClasses
}

sourceSets.main.java.srcDirs += file("build/generated/sources/JSpeccySettings")

dependencies {
    implementation "com.sun.xml.bind:jaxb-core:$jaxbCoreVer"
    implementation "com.sun.xml.bind:jaxb-impl:$jaxbImplVer"
    implementation "javax.xml.bind:jaxb-api:$jaxbApiVer"
    jaxb(
            "com.sun.xml.bind:jaxb-core:$jaxbCoreVer",
            "com.sun.xml.bind:jaxb-xjc:$jaxbXjcVer",
            "javax.xml.bind:jaxb-api:$jaxbApiVer",
    )
    testImplementation "junit:junit:$junitVer"
}

jar.manifest.attributes(
        "Main-Class": 'gui.JSpeccy',
        "Class-Path": configurations.compile
                .collect { it.getName() }
                .join(' ')
)

def nativeLibraryPath = file("native/" + OperatingSystem.current().familyName)

model {
    platforms {
        x86 {
            architecture "x86"
        }
        x64 {
            architecture "x64"
        }
    }
    buildTypes {
        release
    }
    components {
        joystickHelper(NativeLibrarySpec) {
            targetPlatform "x86"
            targetPlatform "x64"
            binaries.all {
                if (targetPlatform.operatingSystem.linux) {
                    cCompiler.args '-I', "${Jvm.current().javaHome}/include"
                    cCompiler.args '-I', "${Jvm.current().javaHome}/include/linux"
                    cCompiler.args '-D_FILE_OFFSET_BITS=64'
                } else if (targetPlatform.operatingSystem.windows) {
                    cCompiler.args "-I${Jvm.current().javaHome}/include"
                    cCompiler.args "-I${Jvm.current().javaHome}/include/win32"
                }
            }
            binaries.withType(StaticLibraryBinarySpec) {
                buildable = false
            }
            binaries.withType(SharedLibraryBinarySpec) {
                buildable = false
                // TODO: Refactor library naming
                def arch = ""
                if (targetPlatform.architecture.name == "x86") {
                    arch = "32"
                } else if (targetPlatform.architecture.name == "x86-64") {
                    arch = "64"
                }
                def os = ""
                def ext = ""
                def name = ""
                if (targetPlatform.operatingSystem.linux) {
                    os = "linux"
                    ext = "so"
                    name = "libJoystickHelper"
                } else if (targetPlatform.operatingSystem.windows) {
                    os = "windows"
                    ext = "dll"
                    name = "joystickHelper"
                }
                sharedLibraryFile = file("native/${os}/${name}-${arch}.${ext}")
            }
            sources.c.source {
                srcDir nativeLibraryPath
                include "*.c"
                include "*.h"
            }
        }
    }
}

test {
    systemProperty "java.library.path", nativeLibraryPath
    testlogger {
        // pick a theme - mocha, standard, plain, mocha-parallel,
        // standard-parallel or plain-parallel
        theme 'plain'
    }
}

run {
    systemProperty "java.library.path", nativeLibraryPath
}

task buildJoystickHelperLib {
    group = 'build'
    dependsOn 'joystickHelperX86SharedLibrary', 'joystickHelperX64SharedLibrary'
}
